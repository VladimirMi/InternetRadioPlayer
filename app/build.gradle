apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def secretsProperties = new Properties()
ext {
    file = new File(rootDir, "secrets.properties")
    if (file.exists()) {
        secretsProperties.load(new FileInputStream(file))
    } else {
        secretsProperties.put("partnerToken", "1234567890")
    }
}

ext.versionMajor = 2
ext.versionMinor = 2
ext.versionPatch = 0

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "io.github.vladimirmi.internetradioplayer"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: 'io.github.vladimirmi.internetradioplayer',
                             "room.schemaLocation"          :
                                     "$projectDir/schemas".toString()]
            }
        }
        buildConfigField("String", "PARTNER_TOKEN", "\"${secretsProperties['partnerToken']}\"")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kapt { generateStubs = true }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}

ext {
    ROOM_VERSION = "2.1.0-alpha04"
    EXOPLAYER_VERSION = "2.9.6"
    OKHHP_VERSION = "3.12.1"
}

dependencies {
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //Support
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.1.0-alpha04'
    implementation 'androidx.media:media:1.1.0-alpha02'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.preference:preference:1.1.0-alpha04'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
    //DB
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    kapt "androidx.room:room-compiler:$ROOM_VERSION"
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
    //Net
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation "com.squareup.retrofit2:converter-gson:2.5.0"
    implementation "com.squareup.okhttp3:okhttp:$OKHHP_VERSION"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0"
    //Rx
    implementation "io.reactivex.rxjava2:rxjava:2.2.7"
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.jakewharton.rxrelay2:rxrelay:2.1.0"
    //DI
    implementation "com.github.stephanenicolas.toothpick:toothpick-runtime:1.1.3"
    kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:1.1.3"
    //Navigation
    implementation "ru.terrakok.cicerone:cicerone:3.0.0"
    //Logging
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.facebook.stetho:stetho:1.5.0"
    implementation "com.squareup.okhttp3:logging-interceptor:$OKHHP_VERSION"
    //ExoPlayer
    implementation "com.google.android.exoplayer:exoplayer-core:$EXOPLAYER_VERSION"
    implementation "com.google.android.exoplayer:extension-okhttp:$EXOPLAYER_VERSION"

    implementation project(':playerbutton')
}
